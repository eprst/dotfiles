#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...


# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/workspace

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${config_files:#(*oh-my-zsh*|*prezto*|*/path.zsh|*/completion.zsh)}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
zmodload -i zsh/complist
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${${(M)config_files:#*/completion.zsh}:#*oh-my-zsh*}
do
  source $file
done

unset config_files
